wf := import("@platforma-sdk/workflow-tengo:workflow")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets:= import("@platforma-sdk/workflow-tengo:assets")

xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
render := import("@platforma-sdk/workflow-tengo:render")
slices := import("@platforma-sdk/workflow-tengo:slices")
pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec")

umapConv := import(":pf-umap-conv")

wf.prepare(func(args){
	// We need a table with cluster ID (optional) | clonotype id | selected ranking columns

	bundleBuilder := wf.createPBundleBuilder()
	bundleBuilder.ignoreMissingDomains() // to make query work for both bulk and single cell data
	bundleBuilder.addAnchor("main", args.inputAnchor) 

	// Add full aa sequence column
	bundleBuilder.addMulti({
		axes: [{ anchor: "main", idx: 1 }],
		annotations: {
			"pl7.app/vdj/isMainSequence": "true"
		},
		domain: {
			"pl7.app/alphabet": "aminoacid"
		}
	}, "aaSequence")

	return {
		columns: bundleBuilder.build()
	}
})

wf.body(func(args) {

	blockId := wf.blockId().getDataAsJson()

	// Input arguments
	columns := args.columns
	datasetSpec := columns.getSpec(args.inputAnchor)
	
	// Needed conditional variables
	isSingleCell := datasetSpec.axesSpec[1].name == "pl7.app/vdj/scClonotypeKey"
	
	// output containers 
	outputs := {}

	// Generate input TSV with Clonotype ID and aa sequence
	umapTable := pframes.tsvFileBuilder()
	umapTable.setAxisHeader(datasetSpec.axesSpec[1].name, "clonotypeKey")
	for col in columns.getColumns("aaSequence") {
		if isSingleCell {
			chainLabel := col.spec.domain["pl7.app/vdj/scClonotypeChain"]
			umapTable.add(col, {header: "aaSequence." + chainLabel})
		} else {
			chainLabel := col.spec.axesSpec[0].domain["pl7.app/vdj/chain"]
			umapTable.add(col, {header: "aaSequence." + chainLabel})
		}
	}
	umapTable.mem("16GiB")
	umapTable.cpu(1)
	umapTable = umapTable.build()


	// Run UMAP analysis
	umapClones := exec.builder().
		software(assets.importSoftware("@platforma-open/milaboratories.clonotype-space.umap:main")).
		mem("32GiB").
		cpu(1).
		addFile("sequences.tsv", umapTable).
		arg("-i").arg("sequences.tsv").
		arg("-u").arg("umap.tsv").
		saveFile("umap.tsv").
		printErrStreamToStdout().
		saveStdoutContent().
		cache(24 * 60 * 60 * 1000).
		run()

	umapPf := xsv.importFile(
		umapClones.getFile("umap.tsv"),
		"tsv",
		umapConv.getColumns(datasetSpec, blockId), 
		{ splitDataAndSpec: true, cpu: 1, mem: "16GiB" })

	// Make trace
	trace := pSpec.makeTrace(datasetSpec,
		{
			type: "milaboratories.clonotype-space",
			importance: 30,
			label: "Clonotype Space"
		})

	pf := pframes.pFrameBuilder()
	for k, v in umapPf {
		pf.add(k, trace.inject(v.spec), v.data)
	}
		
	// Build the final PFrame
	pf = pf.build()

	return {
		outputs: {
			umapPf: pframes.exportFrame(pf)
		},
		exports: {
			umapPf: pf
		}
	}
})
