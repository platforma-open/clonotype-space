wf := import("@platforma-sdk/workflow-tengo:workflow")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets:= import("@platforma-sdk/workflow-tengo:assets")
smart := import("@platforma-sdk/workflow-tengo:smart")
ll := import("@platforma-sdk/workflow-tengo:ll")
render := import("@platforma-sdk/workflow-tengo:render")

xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
pframes := import("@platforma-sdk/workflow-tengo:pframes")

slices := import("@platforma-sdk/workflow-tengo:slices")
umapConv := import(":pf-umap-conv")

pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec")
umapCalculationTpl := assets.importTemplate(":umap-calculation")

wf.prepare(func(args){
	// We need a table with clonotype id and selected sequence column

	bundleBuilder := wf.createPBundleBuilder()
	bundleBuilder.ignoreMissingDomains() // to make query work for both bulk and single cell data
	bundleBuilder.addAnchor("main", args.inputAnchor)

	// Add user-selected sequence feature
	bundleBuilder.addSingle(args.sequenceFeatureRef)

	return {
		columns: bundleBuilder.build()
	}
})

wf.body(func(args) {

	blockId := wf.blockId().getDataAsJson()

	// Input arguments
	columns := args.columns
	datasetSpec := columns.getSpec(args.inputAnchor)
	umap_neighbors := args.umap_neighbors
	umap_min_dist := args.umap_min_dist

	if is_undefined(umap_neighbors) || is_undefined(umap_min_dist) {
		ll.panic("umap_neighbors or umap_min_dist is undefined")
	}

	// Get the selected sequence column
	selectedSeq := columns.getColumn(args.sequenceFeatureRef)
	selectedSeqSpec := selectedSeq.spec

	// Extract alphabet from the sequence spec
	alphabet := selectedSeqSpec.domain["pl7.app/alphabet"]
	if is_undefined(alphabet) {
		ll.panic("Selected sequence does not have alphabet defined in domain")
	}

	// output containers
	outputs := {}

	////////// UMAP //////////
	// Generate input TSV with Clonotype ID and sequence
	umapTable := pframes.tsvFileBuilder()
	umapTable.mem("8GB") // TODO: make this dynamic on input size
	umapTable.setAxisHeader(datasetSpec.axesSpec[1].name, "clonotypeKey")
	umapTable.add(selectedSeq, {header: "sequence"})
	umapTable.mem("16GiB")
	umapTable.cpu(1)
	umapTable = umapTable.build()

	mem := args.mem
	cpu := args.cpu

	// Call UMAP calculation subtemplate
	umapRender := render.create(umapCalculationTpl, {
		umapTable: umapTable,
		umap_neighbors: umap_neighbors,
		umap_min_dist: umap_min_dist,
		alphabet: alphabet,
		mem: mem,
		cpu: cpu
	})

	// Get UMAP file and stdout stream from subtemplate
	umapTsVFile := umapRender.output("umapTsVFile")
	umapOutput := umapRender.output("umapOutput")

	// Import UMAP results to PFrame
	umapPf := xsv.importFile(
		umapTsVFile,
		"tsv",
		umapConv.getColumns(datasetSpec, blockId), 
		{ 
			splitDataAndSpec: true,
			cpu: 1,
			mem: "16GiB" // TODO: make this dynamic on input size
		})

	// Make trace with informative label
	neighborsStr := string(umap_neighbors)
	minDistStr := string(umap_min_dist)

	// Extract sequence feature label
	seqLabel := selectedSeqSpec.annotations["pl7.app/label"]
	if is_undefined(seqLabel) {
		// Fallback to feature name if label not available
		feature := selectedSeqSpec.domain["pl7.app/vdj/feature"]
		if !is_undefined(feature) {
			alphabetSuffix := alphabet == "aminoacid" ? " aa" : " nt"
			seqLabel = string(feature) + alphabetSuffix
		} else {
			seqLabel = "sequence"
		}
	}

	traceLabel := "UMAP (seq:" + seqLabel + ", neighbors:" + neighborsStr + ", min_dist:" + minDistStr + ")"
	
	// Make trace
	trace := pSpec.makeTrace(datasetSpec,
		{
			type: "milaboratories.clonotype-space",
			importance: 20,
			label: traceLabel
		})

	pf := pframes.pFrameBuilder()
	for k, v in umapPf {
		pf.add(k, trace.inject(v.spec), v.data)
	}
		
	// Build the final PFrame
	pf = pf.build()

	return {
		outputs: {
			umapPf: pframes.exportFrame(pf),
			umapOutput: umapOutput
		},
		exports: {
			umapPf: pf
		}
	}
})
